اشمل "مـتم/طـرفية"؛
اشمل "مـتم/مـصفوفة.أسس"؛
اشمل "مـتم/نـص.أسس"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Sqlite"، "إسـكيولايت.أسس")؛

 دالة رئيسي {
    استخدم مـتم؛

    عرف  إسكيولايت: إسـكيولايت.قـب = "mydb.db"؛

    طـرفية.اطبع("SQLite Version: %s\n"،  إسـكيولايت.إصدار())؛
    
    إسكيولايت.نفذ("CREATE table teachers(id INTEGER PRIMARY KEY AUTOINCREMENT, name TEXT, section INT)")؛
    إسكيولايت.نفذ("INSERT INTO teachers VALUES(NULL, 'Mohammed', 1);")؛

    عرف سجلات: مـصفوفة[إسـكيولايت.قـيد] = إسكيولايت.نفذ("SELECT * FROM teachers;")؛
    طـرفية.اطبع("%sROW 0 COLUMN 'NAME': %s\n"، طـرفية.طـراز.ساطع، سجلات(0)("name").صوان)؛
    عرف دليل: صحيح = 0؛
    لكل ، دليل < سجلات (0). مفاتيح.هات_الطول()، دليل++ {
        طـرفية.اطبع("%s %s\t|"، طـرفية.طـراز.ساطع، سجلات(0).مفاتيح(دليل).صوان)
    }

    طـرفية.اطبع("%s\n"، طـرفية.طـراز.صفر)؛
    عرف مدخل_الصف: صحيح = 0؛
    لكل ، مدخل_الصف < سجلات.هات_الطول()، مدخل_الصف++ {
        عرف مدخل_العامود: صحيح = 0؛
        لكل ، مدخل_العامود < سجلات(مدخل_الصف).قيم.هات_الطول()، مدخل_العامود++ {
            طـرفية.اطبع(" %s\t|"، سجلات(مدخل_الصف).قيم(مدخل_العامود).صوان) 
        }
        طـرفية.اطبع("\n")؛
    }
}

رئيسي()؛

